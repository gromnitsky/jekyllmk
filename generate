#!/usr/bin/env wish

array set conf {
    meta.title "Dude's Blog"
    meta.license "Creative Commons Attribution-NoDerivs 3.0"
    meta.sort "descending"

    comp.avatar 1
    comp.about_page 1
    comp.header 0
    comp.footer 1

    feed.baseurl "http://example.com/blog"
    feed.author "Dude"
}

set margin 15
set smargin 5

wm title . "jekyllmk generator"
#wm resizable . 1 0
font configure TkDefaultFont -size 9
font configure TkTextFont -size 9 -family Courier

proc table_row {parent wname text var} {
    global margin
    ttk::label $parent.${wname}_label -text $text
    ttk::entry $parent.$wname -textvariable conf($var.$wname) -width 40
    grid $parent.${wname}_label $parent.$wname -padx $margin -pady "$margin 0"
    grid $parent.${wname}_label -sticky e
    grid $parent.$wname -sticky ew
}

# Meta
ttk::labelframe .meta -text "Meta" -padding "0 0 0 $margin"
table_row .meta title "Site title:" meta
table_row .meta license "License:" meta

ttk::label .meta.sort_label -text "Calendar sort order:"
ttk::frame .meta.sort_radiobutton_frame
ttk::radiobutton .meta.sort_ascending -text "ascending" \
    -variable conf(meta.sort) -value ascending
ttk::radiobutton .meta.sort_descending -text "descending" \
    -variable conf(meta.sort) -value descending
pack .meta.sort_ascending .meta.sort_descending \
    -in .meta.sort_radiobutton_frame -side left -padx "0 $margin"
grid .meta.sort_label .meta.sort_radiobutton_frame -padx $margin -pady "$margin 0"
grid .meta.sort_label -sticky e
grid .meta.sort_radiobutton_frame -sticky w

grid columnconfigure .meta 1 -weight 1
pack .meta -fill x -padx $margin -pady "$margin 0"


# Visual Components
ttk::labelframe .comp -text "Visual Components" -padding "0 0 0 $margin"
ttk::checkbutton .comp.avatar -text "Avatar" -variable conf(comp.avatar)
ttk::checkbutton .comp.about_page -text "About page" \
    -variable conf(comp.about_page)
ttk::checkbutton .comp.header -text "Custom header" -variable conf(comp.header)
ttk::checkbutton .comp.footer -text "Custom footer" -variable conf(comp.footer)

grid .comp.avatar .comp.about_page  -padx $margin -pady "$margin 0"  -sticky w
grid .comp.header .comp.footer  -padx $margin -pady "$margin 0" -sticky w
pack .comp -fill x -padx $margin -pady "$margin 0"


# Atom feed
ttk::labelframe .feed -text "Atom feed" -padding "0 0 0 $margin"
table_row .feed baseurl "Base URL:" feed
table_row .feed author "Author:" feed
grid columnconfigure .feed 1 -weight 1

pack .feed -fill x -padx $margin -pady $margin


# Buttons
frame .buttons
ttk::button .buttons.cancel -text "Cancel" -command {exit 1}
ttk::button .buttons.ok -text "OK" -command ok

pack .buttons.cancel .buttons.ok -side left -padx "$margin 0"
pack .buttons -padx $margin -pady $margin -side right -anchor se


proc ok {} {
    global conf
    parray conf
}
