#!/bin/sh
# the next line restarts using wish \
exec wish "$0" -name jekyllmk_generator ${1+"$@"}

array set gui {
    title "Byron B. Buffington II's Blog"
    license "Creative Commons Attribution-NoDerivs 3.0"
    calendar_treesort "descending"

    feed_baseurl "http://example.com/blog"
    feed_author {Byron "The Buffer" Buffington}

    avatar 1
    about_page 1
    header 0
    footer 1
}

array set conf {}
set conf(dir) [expr {[llength $argv] ? [lindex $argv 0] : [pwd]}]; # Tcl is ugly

set margin 0.3c;		# in centimeters

proc table_row {parent wname text var} {
    global margin
    ttk::label $parent.${wname}_label -text $text
    ttk::entry $parent.$wname -textvariable gui($var) -width 40
    grid $parent.${wname}_label $parent.$wname -padx $margin -pady "$margin 0"
    grid $parent.${wname}_label -sticky e
    grid $parent.$wname -sticky ew
}

proc json_escape {val} {
    return \"[string map [list "\"" "\\\"" \\ \\\\ \b \\b \f \\f \n \\n \r \\r \t \\t] $val]\"
}

proc ok {} {
    global gui

    set r [list]
    foreach key [array names gui] {
	lappend r "[json_escape $key]: [json_escape $gui($key)]"
    }
    puts \{[join $r ",\n"]\}
    exit 0
}


# Main
wm title . "jekyllmk generator"
wm protocol . WM_DELETE_WINDOW {exit 1}
bind . <Escape> {exit 1}
if {[tk windowingsystem] == "x11"} {
    # force all fonts to have a platform-dependent default size
    # according to the DPI
    foreach idx [font names] { font configure $idx -size 0 }
}

# Meta
ttk::labelframe .meta -text "Meta" -padding "0 0 0 $margin"

ttk::label .meta.dir_label -text "Directory:"
ttk::entry .meta.dir -textvariable conf(dir) -state readonly -takefocus 0
grid .meta.dir_label .meta.dir -sticky e -padx $margin -pady "$margin 0"
grid .meta.dir -sticky ew

table_row .meta title "Site title:" title
table_row .meta license "License:" license

ttk::label .meta.sort_label -text "Calendar sort order:"
ttk::frame .meta.sort_radiobutton_frame
ttk::radiobutton .meta.sort_ascending -text "ascending" \
    -variable gui(calendar_treesort) -value ascending
ttk::radiobutton .meta.sort_descending -text "descending" \
    -variable gui(calendar_treesort) -value descending
pack .meta.sort_ascending .meta.sort_descending \
    -in .meta.sort_radiobutton_frame -side left -padx "0 $margin"
grid .meta.sort_label .meta.sort_radiobutton_frame -padx $margin -pady "$margin 0"
grid .meta.sort_label -sticky e
grid .meta.sort_radiobutton_frame -sticky w

grid columnconfigure .meta 1 -weight 1
pack .meta -fill x -padx $margin -pady "$margin 0"


# Visual Components
ttk::labelframe .comp -text "Visual Components" -padding "0 0 0 $margin"
ttk::checkbutton .comp.avatar -text "Avatar" -variable gui(avatar)
ttk::checkbutton .comp.about_page -text "About page" \
    -variable gui(about_page)
ttk::checkbutton .comp.header -text "Custom header" -variable gui(header)
ttk::checkbutton .comp.footer -text "Custom footer" -variable gui(footer)

grid .comp.avatar .comp.about_page  -padx $margin -pady "$margin 0"  -sticky w
grid .comp.header .comp.footer  -padx $margin -pady "$margin 0" -sticky w
pack .comp -fill x -padx $margin -pady "$margin 0"


# Atom feed
ttk::labelframe .feed -text "Atom feed" -padding "0 0 0 $margin"
table_row .feed baseurl "Base URL:" feed_baseurl
table_row .feed author "Author:" feed_author
grid columnconfigure .feed 1 -weight 1

pack .feed -fill x -padx $margin -pady $margin


# Buttons
frame .buttons
ttk::button .buttons.cancel -text "Cancel" -command {exit 1}
ttk::button .buttons.ok -text "OK" -command ok -default active
bind . <Return> [list .buttons.ok invoke]
focus .buttons.ok

pack .buttons.cancel .buttons.ok -side left -padx "$margin 0"
pack .buttons -padx $margin -pady $margin -side right -anchor se

raise .;    # w/o this Windows 10 doesn't give us the focus on startup
